module SPEM2Document;

-- @path src=/SPEM2Doc/sPEM.ecore
-- @path dst=/SPEM2Doc/document.ecore

create OUT : dst from IN : src;

rule spem2Material {
	from
		model : src!SPEM
	to
		mt: dst!Material(
			introduction <- 'this is material of ' + model.modelName,
			checklist <- model.element -> collect(e | thisModule.wbe2CheckList(e)),
			-- 每个文档对应一个环节，也就是WorkBreakdownElement转换为一个Document
			document <- model.element -> collect(e | thisModule.wbe2Document(e))
		)
}

lazy rule wbe2CheckList {
	from 
		wbe: src!WorkBreakdownElement
	to
		cl: dst!CheckList(
			item <- wbe.name + ' document'
		)
		
	
}

lazy rule wbe2Document {
	from 
		wbe : src!WorkBreakdownElement
	to
		dc: dst!Document(
			info <- wbe.description,
			title <- thisModule.wbe2Title(wbe),
			author <- wbe.nested -> collect(e|e.performer) -> flatten()
			-> select(e|e.oclIsTypeOf(src!ProcessPerformer))
			-> collect(e | thisModule.performer2Author(e)),
			section <- wbe.nested -> collect(e | thisModule.activity2Section(e))
		)
}

lazy rule wbe2Title {
	from 
		wbe : src!WorkBreakdownElement
	to
		tt: dst!Title(
			title <- wbe.name
		)
}

lazy rule performer2Author {
	from 
		performer : src!ProcessPerformer
	to
		author : dst!Author(
			name <- performer.name,
			role <- performer.role
		)
}

lazy rule performer2Writer {
	from 
		performer : src!ProcessPerformer
	to
		author : dst!Author(
			name <- performer.name,
			role <- performer.role
		)
}

lazy rule activity2Section {
	from 
		act : src!Acticity
	to
		-- activity 多对多
		sc: dst!Section(
			name <- act.name,
			detail <- thisModule.act2Detail(act)
		)
}

lazy rule act2Detail{
	from 
		act : src!Acticity
	to
		detail: dst!Detail(
			content	<- act.description
		)
}


